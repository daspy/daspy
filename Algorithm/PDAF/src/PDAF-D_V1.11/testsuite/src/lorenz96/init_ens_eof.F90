!$Id: init_ens_eof.F90 859 2010-02-03 09:58:06Z lnerger $
!BOP
!
! !ROUTINE: init_ens_eof --- Initialize ensemble from EOF decomposition
!
! !INTERFACE:
SUBROUTINE init_ens_eof(dim, dim_ens, state, ens, flag)

! !DESCRIPTION:
! User-supplied routine for PDAF (SEIK):
!
! The routine is called by init_seik. It 
! initializes an ensemble of dim\_ens states
! by exact 2nd order sampling.
! State vectors of the form
!   $x_i = x + sqrt(FAC) eofV (\Omega C^{-1})^T$
! fulfill the condition
!   $P = 1/(FAC)  \sum_{i=1}^{dim\_ens} (x_i - x)(x_i - x)^T$
! The matrix is initialized in the form of
! singular values and singular vectors.
!
! This version is for the Lorenz96 model
! without parallelization.
!
! !REVISION HISTORY:
! 2009-11 - Lars Nerger - Initial code
! Later revisions - see svn log
!
! !USES:
  USE mod_memcount, &
       ONLY: memcount
  USE mod_assimilation, &
       ONLY: covartype, file_ini

  IMPLICIT NONE

  INCLUDE 'netcdf.inc'

! !ARGUMENTS:
  INTEGER, INTENT(in) :: dim                 ! PE-local state dimension
  INTEGER, INTENT(in) :: dim_ens             ! Size of ensemble
  REAL, INTENT(inout) :: state(dim)          ! PE-local model state
  ! It is not necessary to initialize the array 'state' for SEIK. 
  ! It is available here only for convenience and can be used freely.
  REAL, INTENT(out)   :: ens(dim, dim_ens)   ! PE-local state ensemble
  INTEGER, INTENT(inout) :: flag             ! PDAF status flag

! !CALLING SEQUENCE:
! Called by: init_seik
! Calls: seik_omega
! Calls: timeit
! Calls: memcount
! Calls: dgemm (BLAS)
!EOP

! *** local variables ***
  INTEGER :: i, s, row, col       ! counters
  INTEGER, SAVE :: allocflag = 0  ! Flag for memory counting
  REAL, ALLOCATABLE :: eofV(:,:)  ! matrix of eigenvectors V 
  REAL, ALLOCATABLE :: svals(:)   ! singular values
  REAL, ALLOCATABLE :: omega(:,:) ! Matrix Omega
  REAL :: fac                     ! Square-root of dim_eof+1 or dim_eof
  INTEGER :: dim_file             ! State dimension in file
  INTEGER :: rank                 ! Rank of approximated covariance matrix
  INTEGER :: rank_file            ! Rank of covariance matrix stored in file
  INTEGER :: stat(50)             ! Array for status flag
  INTEGER :: fileid               ! ID for NetCDF file
  INTEGER :: id_svals, id_eofV    ! IDs for fields
  INTEGER :: id_state             ! ID for field
  INTEGER :: id_dim               ! ID for dimension
  INTEGER :: pos(2)               ! Position index for writing
  INTEGER :: cnt(2)               ! Count index for writing


! **********************
! *** INITIALIZATION ***
! **********************

  ! *** Rank of matrix is ensemble size minus one
  rank = dim_ens - 1
  
  ! *** Generate full ensemble on filter-PE 0 ***
  WRITE (*, '(9x, a)') '--- generate ensemble from covariance matrix'
  WRITE (*, '(9x, a)') &
       '--- use rank reduction and 2nd order exact sampling (SEIK type)'
  WRITE (*, '(9x, a, i5)') '--- Ensemble size:  ', dim_ens
  WRITE (*, '(9x, a, i5)') '--- number of EOFs: ', rank

  ! allocate memory for temporary fields
  ALLOCATE(eofV(dim, rank))
  ALLOCATE(svals(rank))
  ALLOCATE(omega(rank + 1, rank))
  IF (allocflag == 0) THEN
     ! count allocated memory
     CALL memcount(2, 'r', dim * rank + rank + rank * (rank + 1))
  END IF


! *************************************************
! *** Initialize initial state and covar matrix ***
! *************************************************
  
  WRITE(*,'(9x,a,a)') '--- Reading covariance information from ', TRIM(file_ini)

  s = 1
  stat(s) = NF_OPEN(file_ini, NF_NOWRITE, fileid)

  ! Read size of state vector
  s = s + 1
  stat(s) = NF_INQ_DIMID(fileid, 'dim_state', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(fileid, id_dim, dim_file)

  ! Read rank stored in file
  s = s + 1
  stat(s) = NF_INQ_DIMID(fileid, 'rank', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(fileid, id_dim, rank_file)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions from init file, no.', i
  END DO

  ! Check consistency of dimensions
  checkdim: IF (dim == dim_file .AND. rank_file >= rank) THEN

     ! Inquire IDs for mean state, singular vectors and values
     s = 1
     stat(s) = NF_INQ_VARID(fileid, 'meanstate', id_state)
     s = s + 1
     stat(s) = NF_INQ_VARID(fileid, 'u_svd', id_eofV)
     s = s + 1
     stat(s) = NF_INQ_VARID(fileid, 'sigma', id_svals)

     ! Read initialization information
     s = s + 1
     stat(s) = NF_GET_VAR_DOUBLE(fileid, id_state, state)

     pos(2) = 1
     cnt(2) = rank
     pos(1) = 1
     cnt(1) = dim
     s = s + 1
     stat(s) = NF_GET_VARA_DOUBLE(fileid, id_eofV, pos, cnt, eofV)

     s = s + 1
     stat(s) = NF_GET_VARA_DOUBLE(fileid, id_svals, 1, rank, svals)

     s = s + 1
     stat(s) = nf_close(fileid)

     DO i = 1,  s
        IF (stat(i) /= NF_NOERR) &
             WRITE(*, *) 'NetCDF error in reading initialization file, no.', i
     END DO


! *****************************************
! *** Generate ensemble of model states ***
! *****************************************

     WRITE (*,'(9x, a)') '--- generate state ensemble'

     ! *** Generate uniform orthogonal matrix OMEGA ***
     CALL seik_omega(rank, Omega, 1)

     ! ***      Generate ensemble of states         ***
     ! *** x_i = x + sqrt(FAC) eofV (Omega C^(-1))t ***

     ! A = Omega C^(-1)
     DO col = 1, rank
        DO row = 1, rank + 1
           Omega(row, col) = Omega(row, col) * svals(col)
        END DO
     END DO
      
     ! state_ens = state + sqrt(FAC) eofV A^T
     ! FAC depends on the definiton of the factor in the ensemble
     ! covar. matrix which is defined by the variable COVARTYPE
     DO col = 1, rank + 1
        ens(:, col) = state(:)
     END DO

     IF (covartype == 1) THEN
        fac = SQRT(REAL(rank))
     ELSE
        fac = SQRT(REAL(rank + 1))
     END IF

     CALL dgemm('n', 't', dim, rank + 1, rank, &
          fac, eofV, dim, Omega, rank + 1, &
          1.0, ens, dim)
     
  ELSE

      ! *** Rank stored in file is smaller than requested EOF rank ***
     WRITE(*,*) 'Rank stored in file is smaller than requested EOF rank'

     stat(s) = nf_close(fileid)
     STOP

  END IF checkdim


! ****************
! *** clean up ***
! ****************

  DEALLOCATE(svals, eofV, omega)

END SUBROUTINE init_ens_eof
